IF
	Global("Prep","LOCALS",0)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		DisplayString(Myself,39968) // Spell Trigger - Fired
		ApplySpell(Myself,WIZARD_HASTE)
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
		ApplySpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
		ApplySpell(Myself,WIZARD_SPELL_SHIELD)
		ApplySpell(Myself,WIZARD_SPELL_TURNING)
		SetGlobal("Prep","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	StateCheck(Myself,STATE_SILENCED)
	GlobalLT("Vocal","LOCALS",5)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_VOCALIZE)
		IncrementGlobal("Vocal","LOCALS",1)
END

IF
	Detect([PC])
	!See(LastSeenBy(Myself))
	Global("TruSigh","LOCALS",0)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_TRUE_SIGHT)
		SetGlobal("TruSigh","LOCALS",1)
END

IF
	Delay(20)
	!Range("Edwin",20)
	OR(11)
		Global("FigEdw","GLOBAL",1)
		Global("FigEdw","GLOBAL",2)
		Global("FigEdw","GLOBAL",3)
		Global("FigEdw","GLOBAL",4)
		Global("FigEdw","GLOBAL",5)
		Global("FigEdw","GLOBAL",6)
		Global("FigEdw","GLOBAL",7)
		Global("FigEdw","GLOBAL",8)
		Global("FigEdw","GLOBAL",9)
		Global("FigEdw","GLOBAL",10)
		Global("FigEdw","GLOBAL",11)
THEN
	RESPONSE #100
		MoveToObject("Edwin")
END

IF
	Delay(20)
	!Range("Randall",20)
	OR(11)
		Global("FigRan","GLOBAL",1)
		Global("FigRan","GLOBAL",2)
		Global("FigRan","GLOBAL",3)
		Global("FigRan","GLOBAL",4)
		Global("FigRan","GLOBAL",5)
		Global("FigRan","GLOBAL",6)
		Global("FigRan","GLOBAL",7)
		Global("FigRan","GLOBAL",8)
		Global("FigRan","GLOBAL",9)
		Global("FigRan","GLOBAL",10)
		Global("FigRan","GLOBAL",11)
THEN
	RESPONSE #100
		MoveToObject("Randall")
END

IF
	Delay(20)
	!Range("Aerie",20)
	OR(11)
		Global("FigAer","GLOBAL",1)
		Global("FigAer","GLOBAL",2)
		Global("FigAer","GLOBAL",3)
		Global("FigAer","GLOBAL",4)
		Global("FigAer","GLOBAL",5)
		Global("FigAer","GLOBAL",6)
		Global("FigAer","GLOBAL",7)
		Global("FigAer","GLOBAL",8)
		Global("FigAer","GLOBAL",9)
		Global("FigAer","GLOBAL",10)
		Global("FigAer","GLOBAL",11)
THEN
	RESPONSE #100
		MoveToObject("Aerie")
END

IF
	Delay(20)
	!Range(Player1,20)
	OR(11)
		Global("FigPla","GLOBAL",1)
		Global("FigPla","GLOBAL",2)
		Global("FigPla","GLOBAL",3)
		Global("FigPla","GLOBAL",4)
		Global("FigPla","GLOBAL",5)
		Global("FigPla","GLOBAL",6)
		Global("FigPla","GLOBAL",7)
		Global("FigPla","GLOBAL",8)
		Global("FigPla","GLOBAL",9)
		Global("FigPla","GLOBAL",10)
		Global("FigPla","GLOBAL",11)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
	GlobalLT("summon","LOCALS",3)
THEN
	RESPONSE #100
		FaceObject(LastSeenBy(Myself))
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
		IncrementGlobal("summon","LOCALS",1)
END

IF
	!HasBounceEffects(Myself)
	!HasImmunityEffects(Myself)
	HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
END

IF
	HPLT(Myself,30)
	GlobalLT("Potions","LOCALS",5)
THEN
	RESPONSE #100
		UseItem("POTN52",Myself) // Potion of Extra Healing
		IncrementGlobal("Potions","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MISLEAD)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MISLEAD)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_TIME_STOP)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_TIME_STOP)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_GREATER_MALISON)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_GREATER_MALISON)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_SYMBOL_STUN)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_SYMBOL_STUN)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_FINGER_OF_DEATH)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)
END

IF
	HasImmunityEffects(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_DISPEL_MAGIC)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_DISPEL_MAGIC)
END

IF
	HasBounceEffects(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_TRUE_DISPEL_MAGIC)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_TRUE_DISPEL_MAGIC)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_LOWER_RESISTANCE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_LOWER_RESISTANCE)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_PROTECTION_FROM_THE_ELEMENTS)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_PROTECTION_FROM_THE_ELEMENTS)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_FIREBALL)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_ICE_STORM)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_ICE_STORM)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_SUN_FIRE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_SUN_FIRE)
END

IF
	OR(2)
		HasBounceEffects(NearestEnemyOf(Myself))
		HasImmunityEffects(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_BREACH)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_BREACH)
END

IF
	OR(2)
		HasBounceEffects(NearestEnemyOf(Myself))
		HasImmunityEffects(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_PIERCE_MAGIC)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_PIERCE_MAGIC)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_CLOUDKILL)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_CLOUDKILL)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_INCENDIARY_CLOUD)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_INCENDIARY_CLOUD)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MELF_ACID_ARROW)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

IF
	See(NearestEnemyOf(Myself))
	HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END


